
scale=20
pi=3.141592653589793238462643383279

rod_r=24/2
#l0=308		# [mm] length of linear actuator at center point
l0=324		# [mm] length of linear actuator at center point
r1=50.5		# [mm] slide range of linear actuator from center point toward base
r2=50.5		# [mm] slide range of linear actuator from center point toward top
latitude=a(300/425)	# [radian]

# coordinate of pivot on fixed base plate
#l1=l0/2-1
l1=l0/2-21
w1=-s(latitude)*l1
h1=-c(latitude)*l1
d1=450-100/2-1
#d1=450-100/2+3.5
#d1=450-100/2-24-1-1.8

# coordinate of pivot on moving plate
l2=l0/2
w2=s(latitude)*l2
h2=c(latitude)*l2
d2=d1+5

# normal vector for fixed base base
# (w, h, d) = (s(latitude), c(latitude), 0)
normal_w = s(latitude);
normal_h = c(latitude);

# normal vector for moving plate at center point
# (w, h, d) = (s(latitude), c(latitude), 0)

scale=1;
print "; latitude: ",latitude*180/pi,"[deg]\n"
print "; length of linear actuator: ", (l1+l2+r1)/1, "[mm] to ", (l1+l2-r2)/1, "[mm]\n"
scale=20;

pitch=1.75
#gear_ratio=36/8*36/14*36/14*36/14*36/14 * 70/12       # 196.7480216568 6速ギヤボックスHE 
gear_ratio=36/8*36/14*36/14*36/14*36/14*36/14 * 70/12 # 505.9234842603 6速ギヤボックスHE 
#gear_ratio=28*36/12*36/14 * 70/12                     # 216            4速ウォームギヤボックスHE
#gear_ratio=28*36/12*36/14*36/14 * 70/12               # 555.4285714285 4速ウォームギヤボックスHE

delta=5	# [sec], 5, 4, 3, 2, 1
print "LUT_UPDATE_PERIOD\tset\t",delta,"\n";

omega=2.0*pi/86164.09053	# [radian/sec]
rod_w=w2-w1
sqr_w1_w2=rod_w*rod_w

define lt(time){
	auto	omega_t, ht, dt, normal_ht, normal_dt, ip, rod_ht, rod_dt, total_lt, tan_mt, cos_omega_t, sin_omega_t;

	omega_t=omega*time;
	cos_omega_t = c(omega_t);
	sin_omega_t = s(omega_t);

	# pivot point on moving plate
	ht=h2*cos_omega_t - d2*sin_omega_t;
	dt=h2*sin_omega_t + d2*cos_omega_t;

	# normal vector of moving plate
	normal_ht=normal_h*cos_omega_t;
	normal_dt=normal_h*sin_omega_t;

	# connecting rod
	rod_ht=ht-h1;
	rod_dt=dt-d1;

	# total length
	total_lt = sqrt(sqr_w1_w2 + rod_ht*rod_ht + rod_dt*rod_dt);

	# scalar product of connecting rod and normal vector
	ip = rod_w*normal_w + rod_ht*normal_ht + rod_dt*normal_dt;
#	cos_mt = ip/total_lt;
#	tan_mt = sqrt(1.0 - cos_mt*cos_mt)/cos_mt;
	tan_mt = sqrt(total_lt*total_lt - ip*ip)/ip;

	angle_t=a(tan_mt);
	uplift=rod_r*tan_mt;

	ip = rod_w*normal_w + rod_ht*normal_h + rod_dt*normal_d;
	tan_mt = sqrt(total_lt*total_lt - ip*ip)/ip;
	angle_b=a(tan_mt);

	return (total_lt - uplift);
}

# search for starting time
for(start = -delta; lt(start) <= (l1+l2+r2); start = start*2){};
for(n = start/4; n <= -delta; n = n/2){
	if((l1+l2+r2) < lt(start - n)){
		start = start - n;
	}
}

# search for stopping time
for(stop = delta; (l1+l2-r1) <= lt(stop); stop = stop*2){};
for(n = stop/4; delta <= n; n = n/2){
	if(lt(stop - n) < (l1+l2-r1)){
		stop = stop - n;
	}
}

# generate look-up table
n=0
lp=lt(start);
scale=0;
print "\t\t\t\t; ", start/1, "s, ";
scale=3;
print lp/1, "mm, uplift ", uplift/1, "mm\n";
scale=20;
for(time=start+delta; time <= stop; time += delta){
	lc=lt(time);
	rpm=gear_ratio*60*(lp-lc)/pitch/delta;
	scale=0;
	n += 1;
	print "\tLUT_ENTRY\t", (rpm*10+0.5)/1, "\t; ", time/1, "s, ";
	scale=3;
	print lc/1, "mm, uplift ", uplift/1, "mm";
	scale=1;
	print ", top ", angle_t*180/pi, ", base ", angle_b*180/pi;
	scale=3;
	print ", ", rpm/1, "rpm\n";
	scale=20;
	lp=lc;
}

swing=lt(start)-lt(stop)
scale=1
print "\t\t\t; swing: ",swing/1,"mm\n";
scale=0
print "\t\t\t; duration: ",(stop-start)/60,"min ", ((stop-start)%60)/1, "sec\n";
print "\t\t\t; ",n," entries.\n";


